plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT' apply false
    id "com.matthewprenger.cursegradle" version "$cursegradle_version" apply false
    id 'com.matyrobbrt.mc.registrationutils' version "$regVersion"
}

subprojects {
    apply plugin: 'java'

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
//    java.withSourcesJar()
//    java.withJavadocJar()

    jar {
        manifest {
            attributes([
                    'Specification-Title'     : mod_name,
                    'Specification-Vendor'    : mod_author,
                    'Specification-Version'   : project.jar.archiveVersion,
                    'Implementation-Title'    : project.name,
                    'Implementation-Version'  : project.jar.archiveVersion,
                    'Implementation-Vendor'   : mod_author,
                    'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    'Timestampe'              : System.currentTimeMillis(),
                    'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                    'Build-On-Minecraft'      : minecraft_version
            ])
        }
    }

    repositories {
        mavenCentral()

        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }

        maven {
            name = 'BlameJared Maven (CrT / Bookshelf)'
            url = 'https://maven.blamejared.com'
        }
        maven {
            // location of the maven that hosts JEI files
            name = "Progwml6 maven"
            url = "https://dvs1.progwml6.com/files/maven/"
        }
        maven {
            // location of a maven mirror for JEI files, as a fallback
            name = "ModMaven"
            url = "https://modmaven.dev"
        }
    }


    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 17
    }

    // Disables Gradle's custom module metadata from being published to maven. The
    // metadata includes mapped dependencies which are not reasonably consumable by
    // other mod developers.
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    def isForge = project.name == 'Forge'
    def key =  System.getenv('CURSEFORGEKEY')
    println key
    if (project.name != 'Common') {
        apply plugin: 'com.matthewprenger.cursegradle'
        if (!isForge) apply plugin: 'fabric-loom'
        def changelogFile = rootProject.file('CHANGELOG.md').text

        curseforge {
            apiKey = key
            project {
                id = project.projectId
                releaseType = project.releaseType
                changelogType = 'markdown'
                changelog = changelogFile
                addGameVersion project.minecraft_version
                addGameVersion project.name
                mainArtifact(isForge ? jar : remapJar)
            }
            options {
                debug = false
                forgeGradleIntegration = isForge
            }
        }
    }
}

registrationUtils {
    group "lilypuree.${mod_id}.registration"
    projects {
        Fabric { type 'fabric' } // The fabric project
        Forge { type 'forge' } // The forge project
        Common { type 'common' } // The common project
    }
}